## This file can't be generated by Flux using cluster-vars ConfigMap

resource "null_resource" "flux_ocirepository" {
  triggers = {
    asdf_dir                   = coalesce(var.asdf_dir, ".asdf-flux_ocirepository")
    asdf_tools                 = "awscli flux2 kubectl"
    cluster_context            = local.cluster_context
    flux_system_repository_url = local.flux_system_repository_url
    kubeconfig_parameter       = aws_ssm_parameter.kubeconfig.name
    region                     = var.region
  }

  provisioner "local-exec" {
    command = "bash ${path.module}/asdf_install.sh"
    environment = {
      asdf_dir   = self.triggers.asdf_dir
      asdf_tools = self.triggers.asdf_tools
    }
  }

  provisioner "local-exec" {
    command = "bash ${path.module}/flux_ocirepository.sh"
    environment = {
      asdf_dir                   = self.triggers.asdf_dir
      cluster_context            = self.triggers.cluster_context
      flux_system_repository_url = self.triggers.flux_system_repository_url
      kubeconfig_parameter       = self.triggers.kubeconfig_parameter
      region                     = self.triggers.region
    }
  }

  provisioner "local-exec" {
    when    = destroy
    command = "bash ${path.module}/asdf_install.sh"
    environment = {
      asdf_dir   = self.triggers.asdf_dir
      asdf_tools = self.triggers.asdf_tools
    }
  }

  provisioner "local-exec" {
    when    = destroy
    command = "bash ${path.module}/flux_ocirepository_destroy.sh"
    environment = {
      asdf_dir             = self.triggers.asdf_dir
      cluster_context      = self.triggers.cluster_context
      kubeconfig_parameter = self.triggers.kubeconfig_parameter
      region               = self.triggers.region
    }
  }

  depends_on = [
    module.ecr_flux_system,
    null_resource.flux_bootstrap,
  ]
}
